(ns com.sixsq.slipstream.ssclj.resources.session
  (:require
    [com.sixsq.slipstream.ssclj.resources.session-template :as tpl]
    [com.sixsq.slipstream.ssclj.resources.common.std-crud :as std-crud]
    [com.sixsq.slipstream.ssclj.resources.common.schema :as c]
    [com.sixsq.slipstream.ssclj.resources.common.crud :as crud]
    [com.sixsq.slipstream.ssclj.resources.common.utils :as u]
    [com.sixsq.slipstream.ssclj.resources.common.authz :as a]
    [com.sixsq.slipstream.auth.auth :as auth]
    [schema.core :as s]))

(def ^:const resource-tag :sessions)

(def ^:const resource-name "Session")

(def ^:const resource-url (u/de-camelcase resource-name))

(def ^:const collection-name "SessionCollection")

(def ^:const resource-uri (str c/slipstream-schema-uri resource-name))

(def ^:const collection-uri (str c/slipstream-schema-uri collection-name))

(def ^:const create-uri (str c/slipstream-schema-uri resource-name "Create"))

(def collection-acl {:owner {:principal "ADMIN"
                             :type      "ROLE"}
                     :rules [{:principal "ANON"
                              :type      "ROLE"
                              :right     "ALL"}]})

;; FIXME: Need to add correct ACL to resources.
(def resource-acl {:owner {:principal "ADMIN"
                           :type      "ROLE"}})
;;
;; schemas
;;

(def Session
  (merge c/CommonAttrs
         c/AclAttr
         {:username     c/NonBlankString
          :authn-method c/NonBlankString
          :last-active  c/Timestamp}))

(def SessionCreate
  (merge c/CreateAttrs
         {:sessionTemplate tpl/SessionTemplateRef}))

;;
;; multimethods for validation and operations
;;

(def validate-fn (u/create-validation-fn Session))
(defmethod crud/validate resource-uri
  [resource]
  (validate-fn resource))

(def create-validate-fn (u/create-validation-fn SessionCreate))
(defmethod crud/validate create-uri
  [resource]
  (create-validate-fn resource))

(defmethod crud/add-acl resource-uri
  [resource request]
  (a/add-acl resource request))

;;
;; template processing
;;

(defn add-credentials-to-request [request tpl]
  (let [{:keys [authn-method credentials]} tpl
        updated-params (-> request
                           :params
                           (merge credentials)
                           (assoc :authn-method authn-method))]
    (assoc request :params updated-params)))

;; FIXME: Use correct login from authn module.
(defn dummy-login [_]
  {:status   200
   :username "USER_1"
   :cookies  ["COOKIE_1" "COOKIE_2"]})

;; FIXME: Understand how to deal with workflow.
(defn do-login [request]
  (let [{:keys [status username cookies]} (dummy-login request)]
    (if (not= status 200)
      (throw (ex-info "login failed" {:status status :message "login failed"}))
      [username cookies])))

;; FIXME: Understand how to deal with workflow.
(defn tpl->session
  [tpl request]
  (let [tpl (-> tpl
                (dissoc :logo)
                std-crud/resolve-hrefs)
        updated-request (add-credentials-to-request request tpl)
        [username cookies] (do-login updated-request)]
    {:cookies cookies
     :body    {:username     username
               :authn-method (:authn-method tpl)
               :last-active  (u/now)
               :acl          resource-acl}}))

;;
;; CRUD operations
;;

(def add-impl (std-crud/add-fn resource-name collection-acl resource-uri))

;; requires a SessionTemplate to create new Session
(defmethod crud/add resource-name
  [{:keys [body] :as request}]
  (let [tpl (-> body
                (assoc :resourceURI create-uri)
                (crud/validate)
                (:sessionTemplate))
        {:keys [cookies body] :as r} (tpl->session tpl request)]
    (-> (assoc request :body body)
        (add-impl)
        (assoc :cookies cookies))))

(def retrieve-impl (std-crud/retrieve-fn resource-name))

(defmethod crud/retrieve resource-name
  [request]
  (retrieve-impl request))

(def edit-impl (std-crud/edit-fn resource-name))

(defmethod crud/edit resource-name
  [request]
  (edit-impl request))

(def delete-impl (std-crud/delete-fn resource-name))

(defmethod crud/delete resource-name
  [request]
  (delete-impl request))

(def query-impl (std-crud/query-fn resource-name collection-acl collection-uri resource-tag))

(defmethod crud/query resource-name
  [request]
  (query-impl request))
